if (grf_future_status("\F1\25\00\08") == 1 && grf_order_behind("\F1\25\00\08") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "FIRS 4.x when custom station ratings are enabled");
}

if (grf_future_status("\4E\44\59\03") == 1 && grf_order_behind("\4E\44\59\03") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "AIRS when custom station ratings are enabled");
}

if (grf_future_status("\4A\4B\88\08") == 1 && grf_order_behind("\4A\4B\88\08") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "AXIS when custom station ratings are enabled");
}

if (grf_future_status("\4D\47\10\02") == 1 && grf_order_behind("\4D\47\10\02") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "CZIS when custom station ratings are enabled");
}

if (((grf_future_status("\4D\65\6F\91") == 1 && grf_order_behind("\4D\65\6F\91") == 0) ||
    (grf_future_status("\4D\65\6F\94") == 1 && grf_order_behind("\4D\65\6F\94") == 0) ||
    (grf_future_status("\4D\65\6F\95") == 1 && grf_order_behind("\4D\65\6F\95") == 0) ||
    (grf_future_status("\4D\65\6F\96") == 1 && grf_order_behind("\4D\65\6F\96") == 0) ||
    (grf_future_status("\4D\65\6F\97") == 1 && grf_order_behind("\4D\65\6F\97") == 0) ||
    (grf_future_status("\4D\65\6F\98") == 1 && grf_order_behind("\4D\65\6F\98") == 0) ||
    (grf_future_status("\4D\65\6F\99") == 1 && grf_order_behind("\4D\65\6F\99") == 0) ||
    (grf_future_status("\4D\65\6F\9A") == 1 && grf_order_behind("\4D\65\6F\9A") == 0) ||
    (grf_future_status("\4D\65\6F\9C") == 1 && grf_order_behind("\4D\65\6F\9C") == 0) ||
    (grf_future_status("\4D\65\6F\97") == 1 && grf_order_behind("\4D\65\6F\97") == 0) ||
    (grf_future_status("\4D\65\6F\9B") == 1 && grf_order_behind("\4D\65\6F\9B") == 0)) && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "all ECS vectors when custom station ratings are enabled");
}

if (grf_future_status("\54\54\04\01") == 1 && grf_order_behind("\54\54\04\01") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "Improved Town Industries when custom station ratings are enabled");
}

if (grf_future_status("\54\54\32\30") == 1 && grf_order_behind("\54\54\32\30") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "Industries of the Caribbean when custom station ratings are enabled");
}

if (grf_future_status("TT21") == 1 && grf_order_behind("TT21") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "Lumberjack Industries when custom station ratings are enabled");
}

if (grf_future_status("\44\44\06\02") == 1 && grf_order_behind("\44\44\06\02") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "Pikka's Basic Industries when custom station ratings are enabled");
}

if (grf_future_status("\44\44\10\AA") == 1 && grf_order_behind("\44\44\10\AA") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "PIRS when custom station ratings are enabled");
}

if (grf_future_status("\31\FF\05\05") == 1 && grf_order_behind("\31\FF\05\05") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "SPI when custom station ratings are enabled");
}

if (grf_future_status("\4A\44\88\07") == 1 && grf_order_behind("\4A\44\88\07") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "XIS when custom station ratings are enabled");
}

if (grf_future_status("\56\42\03\03") == 1 && grf_order_behind("\56\42\03\03") == 0 && param_station_rating_algorithm != 0) {
    error(FATAL, MUST_LOAD_AFTER, "YETI when custom station ratings are enabled");
}

if (grf_future_status("\4A\4B\88\08") == 1 && param["\4A\4B\88\08", 0] != 0 && param_boredom_penalty != 0) {
    error(WARNING, string(STR_UNSUPPORTED_INCOMPLETE));
}

if (grf_future_status("\4A\44\88\07") == 1 && param["\4A\44\88\07", 0] != 0 && param_boredom_penalty != 0) {
    error(WARNING, string(STR_UNSUPPORTED_INCOMPLETE));
}


is_axis_steeltown = (grf_future_status("\4A\4B\88\08") == 1 && param["\F1\25\00\08", 0] == 0) ? 1 : 0;
is_firs_temperate_basic = (grf_future_status("\F1\25\00\08") == 1 && param["\F1\25\00\08", 0] == 0) ? 1 : 0;
is_firs_arctic_basic = (grf_future_status("\F1\25\00\08") == 1 && param["\F1\25\00\08", 0] == 1) ? 1 : 0;
is_firs_tropic_basic = (grf_future_status("\F1\25\00\08") == 1 && param["\F1\25\00\08", 0] == 2) ? 1 : 0;
is_firs_in_hot_country = (grf_future_status("\F1\25\00\08") == 1 && param["\F1\25\00\08", 0] == 4) ? 1 : 0;
is_ecs_agri = grf_future_status("\4D\65\6F\97");
is_ecs_chemical = grf_future_status("\4D\65\6F\9C");
is_ecs_town = grf_future_status("\4D\65\6F\91");
is_iotc = grf_future_status("\54\54\32\30");
is_lumberjack = grf_future_status("TT21");
is_pbi = grf_future_status("\44\44\06\02");

is_yeti = grf_future_status("\56\42\03\03");
is_spi_sea_cargo = (grf_future_status("\F1\25\00\08") == 1 && param["\F1\25\00\08", 0] == 0) ? 1 : 0;
is_xis_extreme = (grf_future_status("\4A\44\88\07") == 1 && param["\F1\25\00\08", 0] == 0) ? 1 : 0;

has_no_active_industry_set = (grf_future_status("\F1\25\00\08") == 0 && 
    grf_future_status("\4A\44\88\07") == 0 && 
    grf_future_status("\4A\4B\88\08") == 0 && 
    grf_future_status("\4D\47\10\02") == 0 && 
    grf_future_status("\54\54\04\01") == 0 && 
    grf_future_status("\54\54\32\30") == 0 && 
    grf_future_status("TT21") == 0 && 
    grf_future_status("\44\44\06\02") == 0 &&
    grf_future_status("\44\44\10\4A") == 0 &&
    grf_future_status("\4E\44\59\03") == 0 &&
    grf_future_status("\31\FF\05\05") == 0 &&
    grf_future_status("\56\42\03\03") == 0 &&
    grf_future_status("\4D\65\6F\91") == 0 &&
    grf_future_status("\4D\65\6F\94") == 0 &&
    grf_future_status("\4D\65\6F\95") == 0 &&
    grf_future_status("\4D\65\6F\96") == 0 &&
    grf_future_status("\4D\65\6F\97") == 0 &&
    grf_future_status("\4D\65\6F\98") == 0 &&
    grf_future_status("\4D\65\6F\99") == 0 &&
    grf_future_status("\4D\65\6F\9A") == 0 &&
    grf_future_status("\4D\65\6F\9C") == 0 &&
    grf_future_status("\4D\65\6F\97") == 0 &&
    grf_future_status("\4D\65\6F\9B") == 0 ) ? 1 : 0;

// Passenger speed
switch(FEAT_CARGOS, SELF, switch_rating_speed_pass, min(max((getbits(extra_callback_info2, 24, 8) * 100) / (200 - max(min(2000 - current_year, 170),0)) - 52, 0), 50)) {
    return;
}  

// Mail/food speed
switch(FEAT_CARGOS, SELF, switch_rating_speed_mail, min(max((getbits(extra_callback_info2, 24, 8) * 100) / (130 - max(min(2000 - current_year, 110),0)) - 52, 0), 50)) {
    return;
}  


// Other cargo speed
switch(FEAT_CARGOS, SELF, switch_rating_speed, min(max((getbits(extra_callback_info2, 24, 8) * 100) / (112 - max(min(((2000 - current_year)*3)/5, 102),0)) - 52, 0), 50)) {
    return;
}  

switch(FEAT_CARGOS, SELF, switch_rating_1850_waiting, getbits(extra_callback_info2, 8, 16)) {
    0..50: 65;
    51..300: 60;
    301..800: 50;
    801..1200: 30;
    1201..1800: 0;
    1801..2500: -15;
    2501..3400: -50;
    3401..3900: -70;
    -90;
}


switch(FEAT_CARGOS, SELF, switch_rating_1900_waiting, getbits(extra_callback_info2, 8, 16)) {
    0..50: 65;
    51..200: 60;
    201..500: 50;
    501..800: 30;
    801..1250: 0;
    1251..1800: -25;
    1801..3000: -70;
    3001..3800: -90;
    -110;
}


switch(FEAT_CARGOS, SELF, switch_rating_1970_waiting, getbits(extra_callback_info2, 8, 16)) {
    0..50: 65;
    51..100: 60;
    101..300: 50;
    301..600: 30;
    601..1000: 0;
    1001..1500: -35;
    1501..3000: -90;
    -110;
}

switch(FEAT_CARGOS, SELF, switch_rating_2010_waiting, getbits(extra_callback_info2, 8, 16)) {
    0..25: 65;
    26..100: 60;
    101..200: 50;
    201..300: 30;
    301..500: 0;
    501..750: -35;
    751..1000: -90;
    1001..2000: -110;
    -130;
}

switch(FEAT_CARGOS, SELF, switch_rating_2010_waiting_pass, getbits(extra_callback_info2, 8, 16)) {
    0..50: 65;
    51..100: 60;
    101..250: 50;
    251..500: 30;
    501..700: 0;
    701..1000: -35;
    1001..1500: -90;
    1501..2000: -110;
    2001..3000: -130;
    -150;
}

switch(FEAT_CARGOS, SELF, switch_rating_2050_waiting, getbits(extra_callback_info2, 8, 16)) {
    0..25: 65;
    26..50: 60;
    51..100: 50;
    101..200: 30;
    201..350: 0;
    351..500: -35;
    501..750: -90;
    751..1500: -110;
    -130;
}

switch(FEAT_CARGOS, SELF, switch_rating_2050_waiting_pass, getbits(extra_callback_info2, 8, 16)) {
    0..50: 51;
    51..100: 40;
    101..250: 30;
    251..400: 10;
    401..600: 0;
    601..900: -35;
    901..1500: -90;
    1501..2000: -110;
    2001..3000: -130;
    3001..3800: -150;
    -170;
}

switch(FEAT_CARGOS, SELF, switch_rating_1850_pickup, getbits(extra_callback_info2, 0, 8)) {
    0..5: 115;
    6..15: 110;
    16..30: 75;
    31..60: 30;
    61..105: 5;
    0;
}

switch(FEAT_CARGOS, SELF, switch_rating_1900_pickup, getbits(extra_callback_info2, 0, 8)) {
    0..3: 115;
    4..6: 110;
    7..12: 75;
    13..24: 30;
    25..42: 5;
    0;
}

switch(FEAT_CARGOS, SELF, switch_rating_1970_pickup, getbits(extra_callback_info2, 0, 8)) {
    0..1: 115;
    2..3: 110;
    4..6: 75;
    7..12: 30;
    13..21: 5;
    0;
}

switch(FEAT_CARGOS, SELF, switch_boat_aircraft_decay, getbits(extra_callback_info2, 0, 8)) {
    0..100: -50 * param_boredom_penalty;
    101..200: -25 * param_boredom_penalty;
    0;
}

switch(FEAT_CARGOS, SELF, switch_boat_aircraft_penalty, getbits(extra_callback_info1, 0, 8)) {
    0x12: switch_boat_aircraft_decay;
    0x13: switch_boat_aircraft_decay;
    0;
}

switch(FEAT_CARGOS, SELF, switch_boat_penalty, getbits(extra_callback_info1, 0, 8)) {
    0x12: switch_boat_aircraft_decay;
    0;
}


switch(FEAT_CARGOS, SELF, switch_rating_1850, switch_rating_1850_pickup() + switch_rating_1850_waiting() + switch_rating_speed() + switch_boat_aircraft_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1900, switch_rating_1900_pickup() + switch_rating_1900_waiting() + switch_rating_speed() + switch_boat_aircraft_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1970, switch_rating_1970_pickup() + switch_rating_1970_waiting() + switch_rating_speed() + switch_boat_aircraft_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_2010, switch_rating_1970_pickup() + switch_rating_2010_waiting() + switch_rating_speed() + switch_boat_aircraft_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_2050, switch_rating_1970_pickup() + switch_rating_2050_waiting() + switch_rating_speed() + switch_boat_aircraft_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1850_mail, switch_rating_1850_pickup() + switch_rating_1850_waiting() + switch_rating_speed_mail() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1900_mail, switch_rating_1900_pickup() + switch_rating_1900_waiting() + switch_rating_speed_mail() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1970_mail, switch_rating_1970_pickup() + switch_rating_1970_waiting() + switch_rating_speed_mail() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_2010_mail, switch_rating_1970_pickup() + switch_rating_2010_waiting_pass() + switch_rating_speed_mail() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_2050_mail, switch_rating_1970_pickup() + switch_rating_2050_waiting_pass() + switch_rating_speed_mail() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1850_pass, switch_rating_1850_pickup() + switch_rating_1850_waiting() + switch_rating_speed_pass() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1900_pass, switch_rating_1900_pickup() + switch_rating_1900_waiting() + switch_rating_speed_pass() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1970_pass, switch_rating_1970_pickup() + switch_rating_1970_waiting() + switch_rating_speed_pass() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_2010_pass, switch_rating_1970_pickup() + switch_rating_2010_waiting_pass() + switch_rating_speed_pass() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_2050_pass, switch_rating_1970_pickup() + switch_rating_2050_waiting_pass() + switch_rating_speed_pass() + switch_boat_penalty()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1850_sea, switch_rating_1850_pickup() + switch_rating_1850_waiting() + switch_rating_speed()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1900_sea, switch_rating_1900_pickup() + switch_rating_1900_waiting() + switch_rating_speed()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_1970_sea, switch_rating_1970_pickup() + switch_rating_1970_waiting() + switch_rating_speed()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_2010_sea, switch_rating_1970_pickup() + switch_rating_2010_waiting() + switch_rating_speed()) {
    return;
}

switch(FEAT_CARGOS, SELF, switch_rating_2050_sea, switch_rating_1970_pickup() + switch_rating_2050_waiting() + switch_rating_speed()) {
    return;
}



switch(FEAT_CARGOS, SELF, switch_improved_station_rating, current_year) {
    0..1850: switch_rating_1850;
    1851..1900: switch_rating_1900;
    1901..1970: switch_rating_1970;
    1970..2010: switch_rating_2010;
    switch_rating_2050;
}

switch(FEAT_CARGOS, SELF, switch_improved_station_rating_pass, current_year) {
    0..1850: switch_rating_1850_pass;
    1851..1900: switch_rating_1900_pass;
    1901..1970: switch_rating_1970_pass;
    1970..2010: switch_rating_2010_pass;
    switch_rating_2050_pass;
}

switch(FEAT_CARGOS, SELF, switch_improved_station_rating_mail, current_year) {
    0..1850: switch_rating_1850_mail;
    1851..1900: switch_rating_1900_mail;
    1901..1970: switch_rating_1970_mail;
    1970..2010: switch_rating_2010_mail;
    switch_rating_2050_mail;
}


switch(FEAT_CARGOS, SELF, switch_improved_station_rating_sea, current_year) {
    0..1850: switch_rating_1850_sea;
    1851..1900: switch_rating_1900_sea;
    1901..1970: switch_rating_1970_sea;
    1970..2010: switch_rating_2010_sea;
    switch_rating_2050_sea;
}


switch (FEAT_CARGOS, SELF, switch_station_rating_pass, param_station_rating_algorithm) {
 1: switch_improved_station_rating_pass;
 CB_FAILED; // use OpenTTD default
}

switch (FEAT_CARGOS, SELF, switch_station_rating_mail, param_station_rating_algorithm) {
 1: switch_improved_station_rating_mail;
 CB_FAILED; // use OpenTTD default
}

switch (FEAT_CARGOS, SELF, switch_station_rating, param_station_rating_algorithm) {
 1: switch_improved_station_rating;
 CB_FAILED; // use OpenTTD default
}

switch (FEAT_CARGOS, SELF, switch_station_rating_sea, param_station_rating_algorithm) {
 1: switch_improved_station_rating_sea;
 CB_FAILED; // use OpenTTD default
}


// Custom ratings calculations for cargo which may have a boat-only station
switch(FEAT_CARGOS, SELF, switch_station_rating_firs_oil_or_xis_fish, is_firs_in_hot_country || is_xis_extreme ? 1 : 0) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_firs_temperate_basic, is_firs_temperate_basic) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_iotc_or_firs_temperate_basic, is_firs_temperate_basic || is_iotc ? 1 : 0) {
    1: switch_station_rating_sea;
    switch_station_rating;
}


switch(FEAT_CARGOS, SELF, switch_station_rating_firs_arctic_fish, is_firs_arctic_basic) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_iotc_or_firs_tropic_or_xis, is_firs_tropic_basic || is_iotc || is_xis_extreme ? 1 : 0) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_axis_steeltown, is_axis_steeltown) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_ecs_agri_or_spi, is_ecs_agri || is_spi_sea_cargo ? 1 : 0) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_iotc, is_iotc) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_spi, is_spi_sea_cargo) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_xis, is_xis_extreme) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_yeti, is_yeti) {
    1: switch_station_rating_sea;
    switch_station_rating_mail;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_oil, has_no_active_industry_set || is_ecs_chemical || is_lumberjack || is_pbi || is_spi_sea_cargo ? 1 : 0) {
    1: switch_station_rating_sea;
    switch_station_rating;
}

switch(FEAT_CARGOS, SELF, switch_station_rating_tourists, is_ecs_town || is_iotc ? 1 : 0) {
    1: switch_station_rating_pass;
    switch_station_rating;
}

// Switch for each cargo ID
item(FEAT_CARGOS, C_PASS, 0) {
    graphics { station_rating: switch_station_rating_pass; }
}

// ECS tourists
// IotC tourists
item(FEAT_CARGOS, C_1, 1) {
    graphics { station_rating: switch_station_rating_tourists; }
}

item(FEAT_CARGOS, C_MAIL, 2) {
    graphics { station_rating: switch_station_rating_mail; }
}

item(FEAT_CARGOS, C_3, 3) {
    graphics { station_rating: switch_station_rating_oil; }
}

item(FEAT_CARGOS, C_4, 4) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_5, 5) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_6, 6) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_7, 7) {
    graphics { station_rating: switch_station_rating_firs_arctic_fish; }
}

// FIRS4 temperate basic fish
item(FEAT_CARGOS, C_8, 8) {
    graphics { station_rating: switch_station_rating_firs_temperate_basic; }
}

item(FEAT_CARGOS, C_9, 9) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_10, 10) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_FOOD, 11) {
    graphics { station_rating: switch_station_rating_yeti; }
}

// FIRS tropic fish
// IotC food
// XIS clay
item(FEAT_CARGOS, C_12, 12) {
    graphics { station_rating: switch_station_rating_iotc_or_firs_tropic_or_xis; }
}

// IotC imported oil
item(FEAT_CARGOS, C_13, 13) {
    graphics { station_rating: switch_station_rating_iotc; }
}

// ECS Agricultural fish
item(FEAT_CARGOS, C_14, 14) {
    graphics { station_rating: switch_station_rating_ecs_agri_or_spi; }
}

// FIRS4 temperate basic sand
// IotC pipe
item(FEAT_CARGOS, C_15, 15) {
    graphics { station_rating: switch_station_rating_iotc_or_firs_temperate_basic; }
}

// IotC pipe
item(FEAT_CARGOS, C_16, 16) {
    graphics { station_rating: switch_station_rating_iotc; }
}

// SPI sand
item(FEAT_CARGOS, C_17, 17) {
    graphics { station_rating: switch_station_rating_spi; }
}

item(FEAT_CARGOS, C_18, 18) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_19, 19) {
    graphics { station_rating: switch_station_rating; }
}

// IotC fertiliser
item(FEAT_CARGOS, C_20, 20) {
    graphics { station_rating: switch_station_rating_iotc; }
}

// IotC machinery
item(FEAT_CARGOS, C_21, 21) {
    graphics { station_rating: switch_station_rating_iotc; }
}

item(FEAT_CARGOS, C_22, 22) {
    graphics { station_rating: switch_station_rating; }
}

// XIS_fish
item(FEAT_CARGOS, C_23, 23) {
    graphics { station_rating: switch_station_rating_firs_oil_or_xis_fish; }
}

item(FEAT_CARGOS, C_24, 24) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_25, 25) {
    graphics { station_rating: switch_station_rating; }
}

// SPI stone
item(FEAT_CARGOS, C_26, 26) {
    graphics { station_rating: switch_station_rating_spi; }
}

// AXIS steeltown fish
item(FEAT_CARGOS, C_27, 27) {
    graphics { station_rating: switch_station_rating_axis_steeltown; }
}

item(FEAT_CARGOS, C_28, 28) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_29, 29) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_30, 30) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_31, 31) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_32, 32) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_33, 33) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_34, 34) {
    graphics { station_rating: switch_station_rating; }
}

// XIS oil
item(FEAT_CARGOS, C_35, 35) {
    graphics { station_rating: switch_station_rating_xis; }
}

item(FEAT_CARGOS, C_36, 36) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_37, 37) {
    graphics { station_rating: switch_station_rating; }
}

// AXIS steeltown oil
item(FEAT_CARGOS, C_38, 38) {
    graphics { station_rating: switch_station_rating_axis_steeltown; }
}

item(FEAT_CARGOS, C_39, 39) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_40, 40) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_41, 41) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_42, 42) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_43, 43) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_44, 44) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_45, 45) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_46, 46) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_47, 47) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_48, 48) {
    graphics { station_rating: switch_station_rating; }
}

// XIS sand
item(FEAT_CARGOS, C_49, 49) {
    graphics { station_rating: switch_station_rating_xis; }
}

// AXIS steeltown sand
item(FEAT_CARGOS, C_50, 50) {
    graphics { station_rating: switch_station_rating_axis_steeltown; }
}

item(FEAT_CARGOS, C_51, 51) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_52, 52) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_53, 53) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_54, 54) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_55, 55) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_56, 56) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_57, 57) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_58, 58) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_59, 59) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_60, 60) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_61, 61) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_62, 62) {
    graphics { station_rating: switch_station_rating; }
}

item(FEAT_CARGOS, C_63, 63) {
    graphics { station_rating: switch_station_rating; }
}

